<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DevAxRefreshDataApplyAppInsightRegistry</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class DevAxRefreshDataApplyAppInsightRegistry
{ 
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        if(!DevAxRefreshDataHelper::promptConfirmation("@DevAxRefreshData:RFDTA010"))
        {
            return;
        }

        DevAxRefreshDataApplyAppInsightRegistry appInsightRegistry = new DevAxRefreshDataApplyAppInsightRegistry();
        appInsightRegistry.run();
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    public void run()
    {
        if(DevAxRefreshDataHelper::isProductionEnvironment())
        {
            warning("@DevAxRefreshData:RFDTA007");
            return;
        }

        this.updateEnviromentData();
        info("@DevAxRefreshData:RFDTA005");
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateEnviromentData</Name>
				<Source><![CDATA[
    protected void updateEnviromentData()
    {
        DevAxRefreshDataEnvironmentTable environmentTable;
        DevAxRefreshDataTelemetryParameters telemetryParameters;
                         
        ttsbegin;

        str currentUrlHost = DevAxRefreshDataHelper::getCurrentUrlHost();

        while select environmentTable
        {
            str envUrlHost = new System.Uri(environmentTable.envUrl).Host;

            if(envUrlHost == currentUrlHost)
            {
                break;
            }
        }

        if(environmentTable)
        {
            select firstonly telemetryParameters;

            int environmentSelectionInt = environmentTable.EnviornmentMode;
            EnvironmentSelection environmentSelection = environmentSelectionInt;
            OperationalInsightsRegistry insightsRegistry;

            update_recordset insightsRegistry
                  setting  InstrumentationKey = telemetryParameters.SysAppURI,
                           EnvironmentSelection = environmentSelection;
                
            SysIntegrationRegistry integrationRegistry;

            update_recordset integrationRegistry
                  setting SysAppURI = telemetryParameters.SysAppURI,
                          EnvironmentMode = environmentTable.EnviornmentMode;

            SysEnvironmentModeMap modeMap;

            update_recordset modeMap
                  setting SysEnvironmentid = environmentTable.SysEnvironmentid,
                          EnvironmentMode = environmentTable.EnviornmentMode;

            OperationalInsightsEnvMap envMap;
                    
            update_recordset envMap
                  setting EnvironmentId = environmentTable.SysEnvironmentid,
                          EnvironmentSelection = environmentSelection;
        }
                                                        
        ttscommit;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>